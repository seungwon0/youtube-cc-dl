#!/usr/bin/env perl
#
# youtube-cc-dl - Download YouTube Closed Caption
#
# Seungwon Jeong <seungwon0@gmail.com>
#
# Copyright (C) 2011 by Seungwon Jeong

use strict;

use warnings;

use 5.010;

use autodie;

use Getopt::Long;

use IO::Prompt;

use WWW::YouTube::CC;

sub print_version {
    say "youtube-cc-dl $WWW::YouTube::CC::VERSION";
    return;
}

sub print_help {
    print_version();

    print <<'END_HELP';

Usage:

  youtube-cc-dl [options] URL...

Options:

  -h, --help            print this help text and exit
  -v, --version         print program version and exit

Argument:

  URL                   URL of a YouTube video

Examples:

  youtube-cc-dl 'http://www.youtube.com/watch?v=tuRYbBvOMRo'
  youtube-cc-dl k6U-i4gXkLM

Please report bugs to <seungwon0@gmail.com>.
END_HELP
    return;
}

sub get_v_param {
    my ($arg) = @_;

    return if !defined $arg;

    # Argument is a v_param
    return $arg if WWW::YouTube::CC::is_valid_v_param($arg);

    # Argument is a YouTube video URL
    my $v_param = WWW::YouTube::CC::get_v_param_from_url($arg);
    if ( !defined $v_param ) {
        warn "Invalid YouTube video URL: $arg\n";
        return;
    }

    return $v_param;
}

sub get_cc_list {
    my ($v_param) = @_;

    return if !defined $v_param;

    my $xml_ref = WWW::YouTube::CC::download_cc_list($v_param);
    return if !defined $xml_ref;

    my @cc_list = WWW::YouTube::CC::parse_cc_list($xml_ref);
    if ( @cc_list == 0 ) {
        warn "This video seems to have no closed caption.\n";
        return;
    }

    return @cc_list;
}

sub select_cc {
    my ($cc_list_ref) = @_;

    return if !defined $cc_list_ref;

    my $num = @{$cc_list_ref};

    return if $num == 0;

    return $cc_list_ref->[0] if $num == 1;

    # Print closed caption list
    for my $i ( 1 .. $num ) {
        my $name = $cc_list_ref->[ $i - 1 ]{name};
        my $lang = $cc_list_ref->[ $i - 1 ]{lang_code};
        say "[$i] name='$name' / lang_code='$lang'";
    }

PROMPT:
    while (1) {
        my $choice = prompt( "Select a closed caption (1-$num): ", -ti );
        next PROMPT if !defined $choice;
        if ( $choice >= 1 && $choice <= $num ) {
            return $cc_list_ref->[ $choice - 1 ];
        }
    }

    return;
}

sub get_srt {
    my ( $v_param, $cc_ref ) = @_;

    return if !defined $v_param || !defined $cc_ref;

    my $name = $cc_ref->{name};
    my $lang = $cc_ref->{lang_code};

    my $xml_ref = WWW::YouTube::CC::download_cc( $v_param, $name, $lang );
    return if !defined $xml_ref;

    return WWW::YouTube::CC::convert_to_srt($xml_ref);
}

my $print_help;
my $print_version;

GetOptions(
    'help'    => \$print_help,
    'version' => \$print_version,
) or die "GetOptions failed.\n";

if ($print_help) {
    print_help();
    exit;
}

if ($print_version) {
    print_version();
    exit;
}

if ( @ARGV < 1 ) {
    print_help();
    exit 2;
}

ARG:
for my $idx ( 0 .. $#ARGV ) {
    if ( @ARGV > 1 ) {
        printf "[%d/%d] %s\n", $idx + 1, scalar @ARGV, $ARGV[$idx];
    }

    # Get v_param
    my $v_param = get_v_param( $ARGV[$idx] );
    next ARG if !defined $v_param;

    # Download and parse closed caption list
    my @cc_list = get_cc_list($v_param);
    next ARG if @cc_list == 0;

    # Select a closed caption
    my $cc_ref = select_cc( \@cc_list );
    next ARG if !defined $cc_ref;

    # Download and convert closed caption
    my $srt_ref = get_srt( $v_param, $cc_ref );
    next ARG if !defined $srt_ref;

    # Save closed caption into file
    my $file_name = $v_param . '.srt';
    open my $fh, '>', $file_name;
    print {$fh} ${$srt_ref};
    close $fh;
    say "Successfully saved as '$file_name'.";

    if ( @ARGV > 1 ) {
        say q{};
    }
}
